// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/b/RAM16K.hdl

/**
 * Memory of 16K registers, each 16 bit-wide. Out hold the value
 * stored at the memory location specified by address. If load=1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out after the next time step.)
 */

CHIP RAM16K {
    IN in[16], load, address[14];
    OUT out[16];

    PARTS:
    DMux4Way(in=load,sel=address[0..1],a=loada,b=loadb,c=loadc,d=loadd);
    RAM4K(in=in,address=address[2..13],load=loada,out=outa);
    RAM4K(in=in,address=address[2..13],load=loadb,out=outb);
    RAM4K(in=in,address=address[2..13],load=loadc,out=outc);
    RAM4K(in=in,address=address[2..13],load=loadd,out=outd);
    Mux4Way16(a=outa,b=outb,c=outc,d=outd,sel=address[0..1],out=out);
}
